plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
    id "org.sonarqube" version "2.8"
    id 'jacoco'
    id "net.researchgate.release" version "2.6.0"
}

group = 'com.fincher'
description = 'Input / Output channels'
sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenLocal()
    if (project.hasProperty('localNexus')) {
        maven {
            url "${localNexus}"
            allowInsecureProtocol = true
        }
    } else {
        mavenCentral()
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha5'
    implementation 'com.fincher:java-thread:2.2.4'
    implementation 'com.google.guava:guava:29.0-jre'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testImplementation 'org.awaitility:awaitility:4.0.1'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testRuntimeOnly 'org.slf4j:slf4j-log4j12:2.0.0-alpha5'
}

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set("sources")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
	        from components.java
	        artifact tasks.sourcesJar
	    }
    }

    repositories {
        maven {
            credentials {
                username = "${publishUsername}"
                password = "${publishPassword}"
            }
		
            if (project.version.endsWith('-SNAPSHOT')) {
                url "${publishSnapshotUrl}"
            } else {
                url "${publishReleaseUrl}repositories/releases"
            }

            allowInsecureProtocol = true

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

release {
    git {
        requireBranch = ''
    }
}
